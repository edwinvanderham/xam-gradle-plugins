buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin : 'groovy'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

dependencies {
    compile 'commons-io:commons-io:2.4'
    compile localGroovy()
    compile 'org.slf4j:slf4j-api:1.7.21'

    testCompile 'junit:junit:4.11'
    testCompile 'org.assertj:assertj-core:3.2.0'

    testCompile ("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude module : 'groovy-all'
    }

    testRuntime "cglib:cglib-nodep:3.2.4"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.4"    // allows mocking of classes without default constructor (together with CGLIB)
}

repositories {
    mavenCentral()
    mavenLocal()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allGroovy
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? project.property('bintray_user') : null
    key = project.hasProperty('bintray_apikey') ? project.property('bintray_apikey') : null
    pkg {
        repo = 'gradle'
        name = 'xamarin-build-tools'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/oliviergauthier/xam-gradle-plugins'

        version {
            name = project.version
        }
    }
    publications = ['BintrayPublication']
}
